WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
NEWLINE = _{ "\n" | "\r\n" }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }

program = { SOI ~ declaration* ~ EOI }
declaration = _{
    import_decl
    | problem_decl
    | domain_decl
    | interface_decl
    | requirement_decl
    | subproblem_decl
    | world_properties_decl
    | specification_decl
    | requirement_assertions_decl
    | correctness_argument_decl
}

import_decl = { "import" ~ string_literal }
identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
string_literal = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

problem_decl = { "problem:" ~ identifier }

domain_kind = { "biddable" | "causal" | "lexical" }
domain_role = { "given" | "designed" | "machine" }
domain_decl = { "domain" ~ identifier ~ "kind" ~ domain_kind ~ "role" ~ domain_role }

interface_decl = { "interface" ~ string_literal ~ "connects" ~ identifier_list ~ "{" ~ shared_phenomena ~ "}" }
identifier_list = { identifier ~ ("," ~ identifier)+ }
shared_phenomena = { "shared:" ~ "{" ~ phenomenon* ~ "}" }

phenomenon_type = { "event" | "command" | "state" | "value" }
phenomenon = { "phenomenon" ~ identifier ~ ":" ~ phenomenon_type ~ "[" ~ identifier ~ "->" ~ identifier ~ "]" ~ "controlledBy" ~ identifier }

requirement_decl = { "requirement" ~ string_literal ~ "{" ~ req_body ~ "}" }
req_body = { (frame_type | constraint | constrains | reference)* }
frame_type = { "frame:" ~ (identifier | string_literal) }
constraint = { "constraint:" ~ string_literal }
constrains = { "constrains:" ~ identifier }
reference = { "reference:" ~ identifier }

subproblem_decl = { "subproblem" ~ identifier ~ "{" ~ subproblem_body ~ "}" }
subproblem_body = { (subproblem_machine | subproblem_participants | subproblem_requirements)* }
subproblem_machine = { "machine:" ~ identifier }
subproblem_participants = { "participants:" ~ identifier_list }
subproblem_requirements = { "requirements:" ~ string_list }
string_list = { string_literal ~ ("," ~ string_literal)* }

world_properties_decl = { "worldProperties" ~ identifier ~ "{" ~ assertion_stmt* ~ "}" }
specification_decl = { "specification" ~ identifier ~ "{" ~ assertion_stmt* ~ "}" }
requirement_assertions_decl = { "requirementAssertions" ~ identifier ~ "{" ~ assertion_stmt* ~ "}" }
assertion_stmt = { "assert" ~ string_literal ~ assertion_lang? }
assertion_lang = { "@" ~ identifier }

correctness_argument_decl = { "correctnessArgument" ~ identifier ~ "{" ~ prove_stmt ~ "}" }
prove_stmt = { "prove" ~ identifier ~ "and" ~ identifier ~ "entail" ~ identifier }
